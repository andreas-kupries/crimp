# -*- tcl -*-
# -------------------------------------------------------------------------

source [file join \
            [file dirname [file join [pwd] [info script]]] \
            testutilities.tcl]

testsNeedTcl     8.5
testsNeedTcltest 2

support { useLocalFile synth.tcl }
support { useC [mainPath _test/lib]/crimp_core* crimp::core no }
testing { useC [mainPath _test/lib]/crimp*      crimp       no }

# -------------------------------------------------------------------------
## Geometry manipulation. Warp image through a general coordinate field.
## (x/y coordinate fields for result, sampling into the input).
# -------------------------------------------------------------------------

test crimp-warp-field-0.0 {image warp field, wrong\#args} -body {
    crimp warp
} -returnCodes error -result {wrong # args: should be "crimp warp subcommand ?argument ...?"}

test crimp-warp-field-0.1 {image warp field, wrong\#args} -body {
    crimp warp BOGUS
} -returnCodes error -result {unknown or ambiguous subcommand "BOGUS": must be field, or projective}

# -------------------------------------------------------------------------

test crimp-warp-field-1.0 {image warp field, wrong\#args, not enough} -body {
    crimp warp field
} -returnCodes error -result {wrong # args: should be "::crimp::warp::field image xvec yvec"}

test crimp-warp-field-1.1 {image warp field, wrong\#args, not enough} -body {
    crimp warp field IMAGE
} -returnCodes error -result {wrong # args: should be "::crimp::warp::field image xvec yvec"}

test crimp-warp-field-1.2 {image warp field, wrong\#args, not enough} -body {
    crimp warp field IMAGE X
} -returnCodes error -result {wrong # args: should be "::crimp::warp::field image xvec yvec"}

test crimp-warp-field-1.3 {image warp field, wrong\#args, too many} -body {
    crimp warp field IMAGE X Y toomuch
} -returnCodes error -result {wrong # args: should be "::crimp::warp::field image xvec yvec"}

test crimp-warp-field-1.4 {image warp field, invalid arguments, bad option} -body {
    crimp warp field -bogus FOO
} -returnCodes error -result {Expected -interpolate, got "-bogus"}

test crimp-warp-field-1.5 {image warp field, invalid arguments, bad option} -body {
    crimp warp field -interpolate FOO
} -returnCodes error -result {Expected one of nneighbour, bilinear, or bicubic, got "FOO"}

# 3 args: input, x, y
# option -interpolate: bilinear (default), bicubic, nneighbour
# supported inputs: grey8 grey16 grey32 rgb rgba hsv float
# TODO implementation: fpcomplex
# TODO implementation: coordinate samples via single fpcomplex, instead of two separate images.

# Note: coordinate samplers must be convertible to (float x2)
#       => grey8, grey16, grey32, float, but not rgb, rgba, hsv

# -------------------------------------------------------------------------

set n 2
foreach itype [greys] {
    test crimp-warp-field-${n}.0 "[origin] image warp field, nearest neighbour" -body {
        astcl [crimp warp field -interpolate nneighbour [$itype] [sample] [sample]]
        # permutations of the main diagonal
    } -result [iconst $itype 0 0 5 5 {
         0  6 12 18 24
         6 12 18 24  0
        12 18 24  0  6
        18 24  0  6 12
        24  0  6 12 18
    }]

    test crimp-warp-field-${n}.1 "[origin] image warp field, nearest neighbour" -body {
        astcl [crimp warp field -interpolate nneighbour [$itype] [crimp flip transverse [sample]] [sample]]
    } -result [iconst $itype 0 0 5 5 {
         3  7 11 15 24
         7 11 15 24  3
        11 15 24  3  7
        15 24  3  7 11
        24  3  7 11 15
    }]

    # validate...
    test crimp-warp-field-${n}.2 "[origin] image warp field, bilinear (default)" -body {
        astcl [crimp warp field [$itype] [sample] [sample]]
    } -result [iconst $itype 0 0 5 5 {
         1  6 12 20 11
         7 13 18 24  2
        14 19 15  0  6
        18  8  1  7 12
        19  0  8 13 19
    }]

    test crimp-warp-field-${n}.3 "[origin] image warp field, bilinear (default)" -body {
        astcl [crimp warp field [$itype] [crimp flip transverse [sample]] [sample]]
    } -result [iconst $itype 0 0 5 5 {
         4  7 11 17 11
         8 12 15 24  5
        13 16 15  3  7
        15  8  4  8 11
        19  3  9 12 16
    }]

    test crimp-warp-field-${n}.4 "[origin] image warp field, bicubic" -body {
        astcl [crimp warp field -interpolate bicubic [$itype] [sample] [sample]]

    } -result [iconst $itype 0 0 5 5 {
         2  8 13 23  8
         8 14 22 12  2
        14 23  9  1  7
        21  7  2  8 14
        10  1  8 14 22
    }]

    test crimp-warp-field-${n}.5 "[origin] image warp field, bicubic" -body {
        astcl [crimp warp field -interpolate bicubic [$itype] [crimp flip transverse [sample]] [sample]]
    } -result [iconst $itype 0 0 5 5 {
         5  9 12 20  8
         9 13 19 12  6
        13 20  9  5  8
        19  7  5  9 13
        10  5  9 13 19
    }]

    incr n
}

# -------------------------------------------------------------------------
## Handling of input location: Always (0,0).

# TODO

# -------------------------------------------------------------------------
testsuiteCleanup

# Local variables:
# mode: tcl
# indent-tabs-mode: nil
# End:
