# -*- tcl -*-
# -------------------------------------------------------------------------

source [file join \
            [file dirname [file join [pwd] [info script]]] \
            testutilities.tcl]

testsNeedTcl     8.5
testsNeedTcltest 2

support { useLocalFile synth.tcl }
support { useC [mainPath _test/lib]/crimp_core* crimp::core no }
testing { useC [mainPath _test/lib]/crimp*      crimp       no }

# -------------------------------------------------------------------------
## Point operation, maximum at each point.
# -------------------------------------------------------------------------

test crimp-max-1.0 {image max, wrong\#args, not enough} -body {
    crimp max
} -returnCodes error -result {wrong # args: should be "crimp max a b"}

test crimp-max-1.1 {image max, wrong\#args, too many} -body {
    crimp max A B toomuch
} -returnCodes error -result {wrong # args: should be "crimp max a b"}

# -------------------------------------------------------------------------
# MAX (arity 2, commutative)
# Supported combinations (commutations not mentioned):
#          | grey8 grey16 grey32 rgb rgba hsv float fpcomplex double
# ---------+--------------------------------------------------------
# grey8    | *
# grey16   |       *
# grey32   |              *
# rgb      | *                   *
# rgba     | *                   *   *
# hsv      |
# float    | *     *      *                    *
# fpcomplex|
# double   | *     *      *                                   *
# ---------+--------------------------------------------------------
#
#  *--**-*-
#  ------*-
#  ------*-
#  *--**---
#  *--**---
#  --------
#  ***---*-
#  --------
#
# fpcomplex, hsv - Not supported at all
# grey8 works with most of the types.

test crimp-max-2.0 {image max, grey8, self} -body {
    crimp max [grey8] [grey8]
} -result [grey8]

# -------------------------------------------------------------------------
# 16 combinations of t_cross with itself, 8 cardinal directions at 2 displacements each.

foreach {n dx dy result} {
    0  -2 -2 {-2 -2 7 7 {1 . . . 7 . .
                         . 2 . 9 . . .
                         . . 3 . . . 7
                         . 8 . 4 . 9 .
                         6 . . . 5 . .
                         . . . 8 . 4 .
                         . . 6 . . . 5}}
    1  -1 -1 {-1 -1 6 6 {1 . . . 7 .
                         . 2 . 9 . 7
                         . . 3 . 9 .
                         . 8 . 4 . .
                         6 . 8 . 5 .
                         . 6 . . . 5}}
    2   1  1 { 0  0 6 6 {1 . . . 7 .
                         . 2 . 9 . 7
                         . . 3 . 9 .
                         . 8 . 4 . .
                         6 . 8 . 5 .
                         . 6 . . . 5}}
    3   2  2 { 0  0 7 7 {1 . . . 7 . .
                         . 2 . 9 . . .
                         . . 3 . . . 7
                         . 8 . 4 . 9 .
                         6 . . . 5 . .
                         . . . 8 . 4 .
                         . . 6 . . . 5}}
    4  -2  2 {-2  0 7 7 {. . 1 . . . 7
                         . . . 2 . 9 .
                         1 . . . 7 . .
                         . 2 . 9 . 4 .
                         . . 6 . . . 5
                         . 8 . 4 . . .
                         6 . . . 5 . .}}
    5  -1  1 {-1  0 6 6 {. 1 . . . 7
                         1 . 2 . 9 .
                         . 2 . 9 . .
                         . . 8 . 4 .
                         . 8 . 4 . 5
                         6 . . . 5 .}}
    6   1 -1 { 0 -1 6 6 {. 1 . . . 7
                         1 . 2 . 9 .
                         . 2 . 9 . .
                         . . 8 . 4 .
                         . 8 . 4 . 5
                         6 . . . 5 .}}
    7   2 -2 { 0 -2 7 7 {. . 1 . . . 7
                         . . . 2 . 9 .
                         1 . . . 7 . .
                         . 2 . 9 . 4 .
                         . . 6 . . . 5
                         . 8 . 4 . . .
                         6 . . . 5 . .}}
    8   0  1 { 0  0 5 6 {1 . . . 7
                         1 2 . 9 7
                         . 2 3 9 .
                         . 8 3 4 .
                         6 8 . 4 5
                         6 . . . 5}}
    9   0  2 { 0  0 5 7 {1 . . . 7
                         . 2 . 9 .
                         1 . 3 . 7
                         . 8 . 9 .
                         6 . 3 . 5
                         . 8 . 4 .
                         6 . . . 5}}
    10  0 -2 { 0 -2 5 7 {1 . . . 7
                         . 2 . 9 .
                         1 . 3 . 7
                         . 8 . 9 .
                         6 . 3 . 5
                         . 8 . 4 .
                         6 . . . 5}}
    11  0 -1 { 0 -1 5 6 {1 . . . 7
                         1 2 . 9 7
                         . 2 3 9 .
                         . 8 3 4 .
                         6 8 . 4 5
                         6 . . . 5}}
    12  1  0 { 0  0 6 5 {1 1 . . 7 7
                         . 2 2 9 9 .
                         . . 3 3 . .
                         . 8 8 4 4 .
                         6 6 . . 5 5}}
    13  2  0 { 0  0 7 5 {1 . 1 . 7 . 7
                         . 2 . 9 . 9 .
                         . . 3 . 3 . .
                         . 8 . 8 . 4 .
                         6 . 6 . 5 . 5}}
    14 -2  0 {-2  0 7 5 {1 . 1 . 7 . 7
                         . 2 . 9 . 9 .
                         . . 3 . 3 . .
                         . 8 . 8 . 4 .
                         6 . 6 . 5 . 5}}
    15 -1  0 {-1  0 6 5 {1 1 . . 7 7
                         . 2 2 9 9 .
                         . . 3 3 . .
                         . 8 8 4 4 .
                         6 6 . . 5 5}}
} {
    set result [lreplace $result end end [string map {. 0} [lindex $result end]]]

    test crimp-max-3.$n "image max, grey8, ($dx, $dy)" -setup {
        set base  [g8 t_cross]
        set other [crimp rplace $base $dx $dy]
    } -body {
        astcl [crimp max $base $other]
    } -cleanup {
        unset base other
    } -result [iconst grey8 {*}$result]

    test crimp-max-4.$n "image max, grey16, ($dx, $dy)" -setup {
        set base  [g16 t_cross]
        set other [crimp rplace $base $dx $dy]
    } -body {
        astcl [crimp max $base $other]
    } -cleanup {
        unset base other
    } -result [iconst grey16 {*}$result]

    test crimp-max-5.$n "image max, grey32, ($dx, $dy)" -setup {
        set base  [g32 t_cross]
        set other [crimp rplace $base $dx $dy]
    } -body {
        astcl [crimp max $base $other]
    } -cleanup {
        unset base other
    } -result [iconst grey32 {*}$result]
}

# -------------------------------------------------------------------------
## Handling of input location and geometry: Union of inputs.

# -------------------------------------------------------------------------
testsuiteCleanup

# Local variables:
# mode: tcl
# indent-tabs-mode: nil
# End:
