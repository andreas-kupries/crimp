# -*- tcl -*- tcl.tk//DSL tcltest//EN//2.0
## (c) 2016 Andreas Kupries
# # ## ### ##### ######## ############# #####################

kt check Tcl     8.5
kt check tcltest 2

kt source support/synth.tcl

kt local support crimp::core
kt local testing crimp

# -------------------------------------------------------------------------
## Pixel-wise offset.
# -------------------------------------------------------------------------

set n_float  2
set n_double 4

set e_float [iconst float 0 0 5 5 {
     3.00  4.00  5.00  6.00  7.00
     8.00  9.00 10.00 11.00 12.00
    13.00 14.00 15.00 16.00 17.00
    18.00 19.00 20.00 21.00 22.00
    23.00 24.00 25.00 26.00 27.00
}]

set e_double [iconst double 0 0 5 5 {
     3.0000  4.0000  5.0000  6.0000  7.0000
     8.0000  9.0000 10.0000 11.0000 12.0000
    13.0000 14.0000 15.0000 16.0000 17.0000
    18.0000 19.0000 20.0000 21.0000 22.0000
    23.0000 24.0000 25.0000 26.0000 27.0000
}]

foreach itype {grey8 grey16 grey32} {
    set e_$itype [iconst $itype 0 0 5 5 {
         3  4  5  6  7
         8  9 10 11 12
        13 14 15 16 17
        18 19 20 21 22
        23 24 25 26 27
    }]
}

foreach itype [simples] {
    if {[string match grey* $itype] ||
        [string match r*    $itype] ||
        [string match h*    $itype]} {
        set astcl astcl
    } else {
        set astcl [list astclf [set n_$itype]]
    }
    # - - -- --- ----- --------

    test crimp-offset-${itype}-1.0 "image offset, $itype, wrong\#args, not enough" -body {
        crimp offset
    } -returnCodes error -result [wa crimp offset image offset]

    test crimp-offset-${itype}-1.1 "image offset, $itype, wrong\#args, not enough" -body {
        crimp offset IMAGE
    } -returnCodes error -result [wa crimp offset image offset]

    test crimp-offset-${itype}-1.1 "image offset, $itype, wrong\#args, too many" -body {
        crimp offset IMAGE OFF X
    } -returnCodes error -result [wa crimp offset image offset]

    test crimp-offset-${itype}-2.0 "image offset, $itype" -body {
        {*}$astcl [crimp offset [$itype] 3]
    } -result [set e_$itype]

    test crimp-offset-${itype}-3.0 "image offset ($), input handling" -body {
        crimp at [crimp offset [crimp place [$itype] 4 5] 10]
    } -result {4 5}
}

# TODO: unsupported types - errors on unsupported types
# TODO: error on argument type mismatch

# -------------------------------------------------------------------------
cleanupTests

# Local variables:
# mode: tcl
# indent-tabs-mode: nil
# End:
