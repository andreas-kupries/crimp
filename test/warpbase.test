# -*- tcl -*-
# -------------------------------------------------------------------------

source [file join \
            [file dirname [file join [pwd] [info script]]] \
            testutilities.tcl]

testsNeedTcl     8.5
testsNeedTcltest 2

support { useLocalFile synth.tcl }
support { useC [mainPath _test/lib]/crimp_core* crimp::core no }
testing { useC [mainPath _test/lib]/crimp*      crimp       no }

# -------------------------------------------------------------------------
## Geometry manipulation. Warp basics (points, geometries).
# -------------------------------------------------------------------------

test crimp-warp-base-0.0 {image warp, wrong\#args} -body {
    crimp warp
} -returnCodes error -result {wrong # args: should be "crimp warp subcommand ?argument ...?"}

test crimp-warp-base-0.1 {image warp, invalid method} -body {
    crimp warp BOGUS
} -returnCodes error -result {unknown or ambiguous subcommand "BOGUS": must be box, field, point, or projective}

# -------------------------------------------------------------------------

test crimp-warp-point-1.0 {image warp point, wrong\#args, not enough} -body {
    crimp warp point
} -returnCodes error -result {wrong # args: should be "crimp warp point transform ..."}

test crimp-warp-point-1.1 {image warp point, invalid transform} -body {
    crimp warp point T
} -returnCodes error -result {expected projective transform but got "T"}

test crimp-warp-point-1.2 {image warp point, invalid point} -body {
    crimp warp point [crimp transform reflect line {2 4} {4 0}] X
} -returnCodes error -result {wrong # args: should be "crimp::warp_point forwardObj x y"}

test crimp-warp-point-1.3 {image warp point, invalid point} -body {
    crimp warp point [crimp transform reflect line {2 4} {4 0}] {X 1}
} -returnCodes error -result {expected floating-point number but got "X"}

test crimp-warp-point-1.4 {image warp point, invalid point} -body {
    crimp warp point [crimp transform reflect line {2 4} {4 0}] {1 Y}
} -returnCodes error -result {expected floating-point number but got "Y"}

# -------------------------------------------------------------------------
## Simple point transforms. See ptransform.test also.

set n 0
foreach {p pi pm pr ps pt pro pmo pq} {
    { 0  0} { 0  0} { 6.4  3.2} {-0.8284  2     } { 0  0} { 2  3} { 0       0     } { 0  0} {0 0}
    {10  0} {10  0} { 0.4 -4.8} { 6.2426 -5.0711} {20  0} {12  3} { 7.0711 -7.0711} { 0 10} {-3.636363 1.818181}
    { 0 10} { 0 10} {-1.6  9.2} { 6.2426  9.0711} { 0 30} { 2 13} { 7.0711  7.0711} {10  0} {20 -40}
    {10 10} {10 10} {-7.6  1.2} {13.3137  2     } {20 30} {12 13} {14.1421  0     } {10 10} {-1.395348 -1.395348}
} {
    # input ident   reflect     rotate/(2,2)      scale   transl   rotate/origin    reflect/orig quad

    test crimp-warp-point-2.0.$n "image warp point, identity" -setup {
        set t [crimp transform identity]
    } -body {
       lindex [crimp warp point $t $p] 0
    } -cleanup {
        unset t
    } -match 4digits -result $pi

    test crimp-warp-point-2.1.$n "image warp point, reflection" -setup {
        set t [crimp transform reflect line {2 4} {4 0}]
    } -body {
        lindex [crimp warp point $t $p] 0
    } -cleanup {
        unset t
    } -match 4digits -result $pm

    test crimp-warp-point-2.2.$n "image warp point, rotation" -setup {
        set t [crimp transform rotate 45 {2 2}]
    } -body {
        lindex [crimp warp point $t $p] 0
    } -cleanup {
        unset t
    } -match 4digits -result $pr

    test crimp-warp-point-2.3.$n "image warp point, scale" -setup {
        set t [crimp transform scale 2 3]
    } -body {
        lindex [crimp warp point $t $p] 0
    } -cleanup {
        unset t
    } -match 4digits -result $ps

    test crimp-warp-point-2.4.$n "image warp point, translate" -setup {
        set t [crimp transform translate 2 3]
    } -body {
        lindex [crimp warp point $t $p] 0
    } -cleanup {
        unset t
    } -match 4digits -result $pt

    test crimp-warp-point-2.5.$n "image warp point, rotation origin" -setup {
        set t [crimp transform rotate 45]
    } -body {
        lindex [crimp warp point $t $p] 0
    } -cleanup {
        unset t
    } -match 4digits -result $pro

    test crimp-warp-point-2.6.$n "image warp point, reflection origin" -setup {
        set t [crimp transform reflect line {10 10}]
    } -body {
        lindex [crimp warp point $t $p] 0
    } -cleanup {
        unset t
    } -match 4digits -result $pmo

    test crimp-warp-point-2.7.$n "image warp point, quadrilateral" -setup {
        set t [crimp transform quadrilateral \
                   {{0 0} {0  1} {1 1} {1  0}} \
                   {{0 0} {-1 2} {3 3} {4 -2}}]
    } -body {
        lindex [crimp warp point $t $p] 0
    } -cleanup {
        unset t
    } -match 4digits -result $pq

    incr n
}

# -------------------------------------------------------------------------

test crimp-warp-box-3.0 {image warp box, wrong\#args, not enough} -body {
    crimp warp box
} -returnCodes error -result {wrong # args: should be "crimp warp box transform geometry"}

test crimp-warp-box-3.1 {image warp box, wrong\#args, not enough} -body {
    crimp warp box T
} -returnCodes error -result {wrong # args: should be "crimp warp box transform geometry"}

test crimp-warp-box-3.2 {image warp box, wrong\#args, too many} -body {
    crimp warp box T G toomuch
} -returnCodes error -result {wrong # args: should be "crimp warp box transform geometry"}

test crimp-warp-box-3.3 {image warp box, invalid transform} -body {
    crimp warp box T G
} -returnCodes error -result {expected projective transform but got "T"}

test crimp-warp-box-3.4 {image warp box, invalid box} -body {
    crimp warp box [crimp transform reflect line {2 4} {4 0}] G
} -returnCodes error -result {wrong # args: should be "crimp::warp_box forwardObj x y w h"}

test crimp-warp-box-3.5 {image warp box, invalid box} -body {
    crimp warp box [crimp transform reflect line {2 4} {4 0}] {X 1 5 5}
} -returnCodes error -result {expected integer but got "X"}

test crimp-warp-box-3.6 {image warp box, invalid box} -body {
    crimp warp box [crimp transform reflect line {2 4} {4 0}] {1 Y 5 5}
} -returnCodes error -result {expected integer but got "Y"}

test crimp-warp-box-3.7 {image warp box, invalid box} -body {
    crimp warp box [crimp transform reflect line {2 4} {4 0}] {1 1 W 5}
} -returnCodes error -result {expected integer but got "W"}

test crimp-warp-box-3.8 {image warp box, invalid box} -body {
    crimp warp box [crimp transform reflect line {2 4} {4 0}] {1 1 5 H}
} -returnCodes error -result {expected integer but got "H"}

# -------------------------------------------------------------------------
testsuiteCleanup

# Local variables:
# mode: tcl
# indent-tabs-mode: nil
# End:
