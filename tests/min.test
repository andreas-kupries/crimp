# -*- tcl -*- tcl.tk//DSL tcltest//EN//2.0
## (c) 2011-2016 Andreas Kupries
# # ## ### ##### ######## ############# #####################

kt check Tcl     8.5
kt check tcltest 2

kt source support/synth.tcl

kt local support crimp::core
kt local testing crimp

# -------------------------------------------------------------------------
## Point operation, minimum at each point.
# -------------------------------------------------------------------------

test crimp-min-1.0 {image min, wrong\#args, not enough} -body {
    crimp min
} -returnCodes error -result [wa crimp min a b]

test crimp-min-1.1 {image min, wrong\#args, too many} -body {
    crimp min A B toomuch
} -returnCodes error -result [wa crimp min a b]

# -------------------------------------------------------------------------
# MIN (arity 2, commutative)
# Supported combinations (commutations not mentioned):
#          | grey8 grey16 grey32 rgb rgba hsv float fpcomplex double
# ---------+--------------------------------------------------------
# grey8    | *
# grey16   |       *
# grey32   |              *
# rgb      | *                   *
# rgba     | *                   *   *
# hsv      |
# float    | *     *      *                    *
# fpcomplex|
# double   | *     *      *                                   *
# ---------+--------------------------------------------------------
#
#  *--**-*-
#  ------*-
#  ------*-
#  *--**---
#  *--**---
#  --------
#  ***---*-
#  --------
#
# fpcomplex, hsv - Not supported at all
# grey8 works with most of the types.

test crimp-min-2.0 {image min, grey8, self} -body {
    crimp min [grey8] [grey8]
} -result [grey8]

# -------------------------------------------------------------------------
# 16 combinations of t_cross with itself, 8 cardinal directions at 2 displacements each.

foreach {n dx dy result} {
    0  -2 -2 {-2 -2 
        _______
        _______
        __1..__
        __.2.__
        __..3__
        _______
        _______}

    1  -1 -1 {-1 -1 
        ______
        _1.9._
        _.2.9_
        _8.3._
        _.8.4_
        ______}

    2   1  1 { 0  0 
        ______
        _1.9._
        _.2.9_
        _8.3._
        _.8.4_
        ______}

    3   2  2 { 0  0 
        _______
        _______
        __1..__
        __.2.__
        __..3__
        _______
        _______}

    4  -2  2 {-2  0 
        _______
        _______
        __..3__
        __.8.__
        __3..__
        _______
        _______}

    5  -1  1 {-1  0 
        ______
        _.2.7_
        _2.3._
        _.3.4_
        _6.4._
        ______}

    6   1 -1 { 0 -1 
        ______
        _.2.7_
        _2.3._
        _.3.4_
        _6.4._
        ______}

    7   2 -2 { 0 -2 
        _______
        _______
        __..3__
        __.8.__
        __3..__
        _______
        _______}

    8   0  1 { 0  0 
        _____
        12.97
        .239.
        .834.
        68.45
        _____}

    9   0  2 { 0  0 
        _____
        _____
        1.3.7
        .2.4.
        6.3.5
        _____
        _____}

    10  0 -2 { 0 -2 
        _____
        _____
        1.3.7
        .2.4.
        6.3.5
        _____
        _____}

    11  0 -1 { 0 -1 
        _____
        12.97
        .239.
        .834.
        68.45
        _____}

    12  1  0 { 0  0 
        _1..7_
        _2299_
        _.33._
        _8844_
        _6..5_}

    13  2  0 { 0  0 
        __1.7__
        __.2.__
        __3.3__
        __.4.__
        __6.5__}

    14 -2  0 {-2  0 
        __1.7__
        __.2.__
        __3.3__
        __.4.__
        __6.5__}

    15 -1  0 {-1  0 
        _1..7_
        _2299_
        _.33._
        _8844_
        _6..5_}
} {
    set result [gconst {*}$result]

    test crimp-min-3.$n "image min, grey8, ($dx, $dy)" -setup {
        set base  [g8 t_cross2]
        set other [crimp rplace $base $dx $dy]
    } -body {
        astcl [crimp min $base $other]
    } -cleanup {
        unset base other
    } -result [iconst grey8 {*}$result]

    test crimp-min-4.$n "image min, grey16, ($dx, $dy)" -setup {
        set base  [g16 t_cross2]
        set other [crimp rplace $base $dx $dy]
    } -body {
        astcl [crimp min $base $other]
    } -cleanup {
        unset base other
    } -result [iconst grey16 {*}$result]

    test crimp-min-5.$n "image min, grey32, ($dx, $dy)" -setup {
        set base  [g32 t_cross2]
        set other [crimp rplace $base $dx $dy]
    } -body {
        astcl [crimp min $base $other]
    } -cleanup {
        unset base other
    } -result [iconst grey32 {*}$result]
}

# -------------------------------------------------------------------------
## Handling of input location and geometry: Union of inputs.

# -------------------------------------------------------------------------
cleanupTests

# Local variables:
# mode: tcl
# indent-tabs-mode: nil
# End:
