# -*- tcl -*- tcl.tk//DSL tcltest//EN//2.0
## (c) 2011-2016 Andreas Kupries
# # ## ### ##### ######## ############# #####################

kt check Tcl     8.5
kt check tcltest 2

kt source support/synth.tcl

kt local testing crimp::core

# -------------------------------------------------------------------------
## Core math: Linear interpolation

test crimp-core-math-1linear-1.0 {interpolate linear, wrong args, not enough} -body {
    crimp interpolate linear
} -returnCodes error -result {wrong # args: should be "crimp interpolate linear a b xf"}

test crimp-core-math-1linear-1.1 {interpolate linear, wrong args, not enough} -body {
    crimp interpolate linear A
} -returnCodes error -result {wrong # args: should be "crimp interpolate linear a b xf"}

test crimp-core-math-1linear-1.2 {interpolate linear, wrong args, not enough} -body {
    crimp interpolate linear A B
} -returnCodes error -result {wrong # args: should be "crimp interpolate linear a b xf"}

test crimp-core-math-1linear-1.3 {interpolate linear, wrong args, too many} -body {
    crimp interpolate linear A B XF XXX
} -returnCodes error -result {wrong # args: should be "crimp interpolate linear a b xf"}

# -------------------------------------------------------------------------
## Fixed functionality

test crimp-core-math-1linear-2.0 {interpolate linear, fixed setup} -body {
    crimp interpolate linear 0 1 0.5
} -result 0.5 -match epsilon

# -------------------------------------------------------------------------
## Randomized functionality.

apply {{} {
    set n 0
    foreach _ [iota 500] {
        # Compute an interpolation.
        set a [rand -10 10]
        set b [rand -10 10]
        set x [rand 0 1]
        set y [expr {$a+($b-$a)*$x}]

        # And check
        test crimp-core-math-1linear-3.$n "interpolate linear, fuzz" -body {
            crimp interpolate linear $a $b $x
        } -result $y -match 14digits

        unset a b x y
        incr n
    }
}}

# -------------------------------------------------------------------------
cleanupTests

# Local variables:
# mode: tcl
# indent-tabs-mode: nil
# End:
